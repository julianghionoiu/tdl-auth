group 'ro.ghionoiu'
//DEBT Remember to also change the version in ./deploy.sh
version '0.0.2'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'ch.netzwerg:gradle-release-plugin:1.2.4'
    }

}

apply plugin: 'java'
apply plugin: 'application'

//noinspection GroovyUnusedAssignment
sourceCompatibility = JavaVersion.VERSION_1_8
//noinspection GroovyUnusedAssignment
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.amazonaws:aws-lambda-java-core:1.1.0'
    compile 'com.amazonaws:aws-java-sdk-sts:1.11.135'
    compile 'com.amazonaws:aws-java-sdk-ses:1.11.135'
    compile 'com.amazonaws:aws-java-sdk-s3:1.11.135'
    compile 'ro.ghionoiu:kms-jwt:0.0.3'

    compile 'com.beust:jcommander:1.64'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.json:json:20140107'
    
    compile 'org.freemarker:freemarker:2.3.14'

    compileOnly 'org.projectlombok:lombok:1.16.16'


    testCompile('junit:junit:4.11'){
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'ro.ghionoiu:s3-sync-stream:0.0.8'
    testCompile 'ch.qos.logback:logback-classic:1.1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.powermock:powermock-api-mockito:1.7.0RC4'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0RC4'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.7.0RC4'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
}


apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "tdl.auth.CredentialsApp"
jar {
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes "Main-Class": "$mainClassName"
    }
}


// ~~~~~ Gather coverage

apply plugin: "jacoco"
apply plugin: "com.github.kt3k.coveralls"

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task buildZip(type: Zip) {
    baseName = "tdl-auth-lambda"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}


build.dependsOn buildZip
